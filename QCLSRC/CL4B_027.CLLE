PGM     PARM(&FILENAME)
DCL     VAR(&FILENAME)    TYPE(*CHAR) LEN(60)
DCL     VAR(&MODE)        TYPE(*CHAR) LEN(11)  VALUE('rr')
DCL     VAR(&NULL)        TYPE(*CHAR) LEN(1)   VALUE(X'00')
DCL     VAR(&FP)          TYPE(*PTR)  ADDRESS(*NULL)
INCLUDE SRCSTMF('RFILE_P.CLLE')
DCL     VAR(&RIOFB_P)     TYPE(*PTR)  ADDRESS(*NULL)
INCLUDE SRCSTMF('RIOFB_P.CLLE')
DCL     VAR(&RAW_ROW)     TYPE(*CHAR) LEN(312) 
DCL     VAR(&ROW)         TYPE(*CHAR) LEN(300) +
        STG(*DEFINED) DEFVAR(&RAW_ROW 13)  
DCL     VAR(&OPTS)        TYPE(*INT)  LEN(4)   VALUE(0)
DCL     VAR(&EFF_LEN)     TYPE(*INT)  LEN(4)   VALUE(-12)
CHGVAR  VAR(&FILENAME)  VALUE(&FILENAME *TCAT &NULL)
CHGVAR  VAR(&MODE)      VALUE(&MODE *TCAT &NULL)
CALLPRC PRC('_Ropen')   PARM(&FILENAME &MODE) RTNVAL(&FP)
CHGVAR  VAR(&EFF_LEN)   VALUE(&EFF_LEN + &F10_BUFLEN) 
CALLPRC PRC('_Rreadn')  +
        PARM((&FP *BYVAL) +
             &RAW_ROW +
             (&F10_BUFLEN *BYVAL) +
             (&OPTS *BYVAL)) +
        RTNVAL(&RIOFB_P)
DOWHILE COND(&F06_NUMBYT *NE -1)
  SNDPGMMSG MSG(%SST(&ROW 1 &EFF_LEN))
  CALLPRC PRC('_Rreadn')  +
          PARM((&FP *BYVAL) +
               &RAW_ROW +
               (&F10_BUFLEN *BYVAL) +
               (&OPTS *BYVAL)) +
          RTNVAL(&RIOFB_P)
ENDDO
CALLPRC PRC('_Rclose')  PARM((&FP *BYVAL))
ENDPGM
